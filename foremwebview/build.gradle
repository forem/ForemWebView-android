plugins {
  id 'com.android.library'
  id 'kotlin-android'
  id 'maven-publish'
}
ext{
  version_code = 12
  version_name = "1.0.12"
}

android {
  compileSdk 31

  defaultConfig {
    minSdk 21
    targetSdk 31
    versionCode version_code
    versionName "$version_name"

    buildConfigField("String", "ANDROID_BRIDGE", "\"AndroidBridge\"")
    buildConfigField("String", "FOREM_AGENT_EXTENSION", "\"ForemWebView/$version_name\"")
    buildConfigField("String", "PASSPORT_AGENT_EXTENSION", "\"ForemWebView/passport\"")
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

dependencies {
  implementation("androidx.appcompat:appcompat:1.4.2")
  implementation("androidx.constraintlayout:constraintlayout:2.1.4")
  implementation("androidx.fragment:fragment-ktx:1.4.1")

  // Glide
  implementation 'com.github.bumptech.glide:glide:4.13.2'
  annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'

  // WebView
  implementation("androidx.browser:browser:1.4.0")

  // Lifecycle
  implementation("androidx.lifecycle:lifecycle-extensions:2.2.0")

  // Exoplayer
  implementation("com.google.android.exoplayer:exoplayer-core:2.17.1")
  implementation("com.google.android.exoplayer:exoplayer-hls:2.17.1")
  implementation("com.google.android.exoplayer:extension-mediasession:2.17.1")
  implementation("com.google.android.exoplayer:exoplayer-ui:2.17.1")

  // Gson
  implementation("com.squareup.retrofit2:converter-gson:2.9.0")

  // Push notifications - FCM
  implementation("com.google.firebase:firebase-analytics:21.0.0")
  implementation("com.google.firebase:firebase-messaging:23.0.5")

  testImplementation(
      'android.arch.core:core-testing:1.1.1',
      'androidx.test.espresso:espresso-core:3.2.0',
      'androidx.test.ext:junit:1.1.1',
      'androidx.work:work-testing:2.4.0',
      'com.google.dagger:dagger:2.24',
      'com.google.truth.extensions:truth-liteproto-extension:1.1.3',
      'com.google.truth:truth:1.1.3',
      'com.squareup.okhttp3:mockwebserver:4.7.2',
      'junit:junit:4.12',
      "org.jetbrains.kotlin:kotlin-test-junit:1.4.20",
      'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2',
      'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.2',
      'org.mockito:mockito-core:3.9.0',
      'org.robolectric:robolectric:4.5',
  )
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release

        groupId = 'com.github.forem'
        artifactId = 'ForemWebView-android'
        version = "$version_name"
      }
    }
  }
}
